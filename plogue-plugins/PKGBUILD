# Maintainer: Katie Wolfe <~kt/public-inbox@lists.sr.ht>
pkgname=('plogue-aria' 'plogue-chipspeech' 'plogue-chipsounds' 'plogue-sforzando' 'plogue-tablewarp2')
pkgbase=plogue-plugins
pkgver='1.982.beta3'
debver='1.982~beta3'
_dirver="1.982beta3"
license=('custom')
pkgrel=1
epoch=1
groups=('pro-audio')
pkgdesc="Plogue audio plugins, beta Linux port (unofficial support, DO NOT report bugs upstream!)"
arch=('x86_64')
url="https://plogue.com"
options=('!strip')
install=plogue-plugins.install
source_x86_64=(
	"https://chipsounds.s3.us-east-1.amazonaws.com/LINUX_plogue-chipsounds_${debver}_x86_64.zip"
	"https://chipspeech.s3.us-east-1.amazonaws.com/LINUX_plogue-chipspeech_${debver}_x86_64.zip"
	"https://sforzando.s3.us-east-1.amazonaws.com/LINUX_plogue-sforzando_${debver}_x86_64.zip"
)
noextract=()
sha512sums_x86_64=('90ced79317888d4231fe389f79225b5d8d6ae945c0cebeb5a22fa0f4144e623f0a93a85e66287526ca719384ca5d183dc79cb84c2673a6c2e59a9718c201d418'
					'153b29b176739045b5adeed8cbc9ae43e1ad780e12a346cf92ab08afedcf7a2fbaaa082c8896369f5696da8372936449e0d62c0cb4f66236aabb27c44bcdb7fd'
					'ea40781b86caf10e5e9e8ec9489310908c283339044b8ea1b2739030ba9c7013dda4efbe51c9ee5eb34964293504ab84cf4802a37afaf0453b2ec3b589c8d235')

debs=('aria' 'chipsounds' 'chipspeech' 'sforzando' 'tablewarp2')

package_plogue-aria() {
	# from deb: libgtk-3-0,libglib2.0-0,libpango-1.0-0,libcairo2,libpangocairo-1.0-0,curl
	depends=('gtk3mm' 'glib2' 'pango' 'cairo' 'curl' 'zenity')
	optdepends=('libsndfile: Adds support for more audio formats')

	mkdir -p "$srcdir/${pkgname}"
	cd "$srcdir/${pkgname}"
	ar x "$srcdir/LINUX_plogue-chipspeech_${_dirver}_x86_64/${pkgname}_${debver}_amd64.deb"
	tar -C "${pkgdir}" -xJf "$srcdir/$pkgname/data.tar.xz"
	install -Dm 644 "${pkgdir}/usr/share/doc/${pkgname}/copyright" "${pkgdir}/usr/share/licenses/${pkgname}/copyright"
}

package_plogue-chipspeech() {
	depends=('plogue-aria')

	mkdir -p "$srcdir/${pkgname}"
	cd "$srcdir/${pkgname}"
	ar x "$srcdir/LINUX_plogue-chipspeech_${_dirver}_x86_64/${pkgname}_${debver}_amd64.deb"
	tar -C "${pkgdir}" -xJf "$srcdir/$pkgname/data.tar.xz"
	install -Dm 644 "${pkgdir}/usr/share/doc/${pkgname}/copyright" "${pkgdir}/usr/share/licenses/${pkgname}/copyright"
}

package_plogue-chipsounds() {
	depends=('plogue-aria')

	mkdir -p "$srcdir/${pkgname}"
	cd "$srcdir/${pkgname}"
	ar x "$srcdir/LINUX_plogue-chipsounds_${_dirver}_x86_64/${pkgname}_${debver}_amd64.deb"
	tar -C "${pkgdir}" -xJf "$srcdir/$pkgname/data.tar.xz"
	install -Dm 644 "${pkgdir}/usr/share/doc/${pkgname}/copyright" "${pkgdir}/usr/share/licenses/${pkgname}/copyright"
}

package_plogue-sforzando() {
	depends=('plogue-aria')

	mkdir -p "$srcdir/${pkgname}"
	cd "$srcdir/${pkgname}"
	ar x "$srcdir/LINUX_plogue-sforzando_${_dirver}_x86_64/${pkgname}_${debver}_amd64.deb"
	tar -C "${pkgdir}" -xJf "$srcdir/$pkgname/data.tar.xz"
	install -Dm 644 "${pkgdir}/usr/share/doc/${pkgname}/copyright" "${pkgdir}/usr/share/licenses/${pkgname}/copyright"
}
package_plogue-tablewarp2() {
	depends=('plogue-aria')

	mkdir -p "$srcdir/${pkgname}"
	cd "$srcdir/${pkgname}"
	ar x "$srcdir/LINUX_plogue-sforzando_${_dirver}_x86_64/${pkgname}_${debver}_amd64.deb"
	tar -C "${pkgdir}" -xJf "$srcdir/$pkgname/data.tar.xz"
	install -Dm 644 "${pkgdir}/usr/share/doc/${pkgname}/copyright" "${pkgdir}/usr/share/licenses/${pkgname}/copyright"
}

