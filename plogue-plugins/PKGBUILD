# Maintainer: Katie Wolfe <~kt/public-inbox@lists.sr.ht>
pkgname=('plogue-aria' 'plogue-chipspeech' 'plogue-chipsounds' 'plogue-sforzando' 'plogue-tablewarp2')
pkgbase=plogue-plugins
pkgver='1.982.beta6'
debver='1.982~beta6'
_dirver="1.982beta6"
license=('custom')
pkgrel=1
epoch=1
groups=('pro-audio')
pkgdesc="Plogue audio plugins, beta Linux port (unofficial support, DO NOT report bugs upstream!)"
arch=('x86_64')
url="https://plogue.com"
options=('!strip')
install=plogue-plugins.install
source_x86_64=(
	"https://chipsounds.s3.us-east-1.amazonaws.com/LINUX_plogue-chipsounds_${debver}_x86_64.zip"
	"https://chipspeech.s3.us-east-1.amazonaws.com/LINUX_plogue-chipspeech_${debver}_x86_64.zip"
	"https://sforzando.s3.us-east-1.amazonaws.com/LINUX_plogue-sforzando_${debver}_x86_64.zip"
)
noextract=()
sha512sums_x86_64=('feed70428b1ae33b45c7e598830efdd2dbac0a0d27143058724b769e83befdcb8492a633acf317cf943f35d50389b9089fc49addf0b1fff10aa300f0ad2b019e'
					'225c4f0de7d07f6c6ad17714a0cc8b36643b895dce3c21896722ee226ffc42219d99a75b1e7865de2c6fcf055162150c5f89cc8aef6c4371e226d0cc9e9369ef'
					'263690bc7bd2ba0db9797b42557bda69ae9d5c614d48b5dda9bf6f42834bf1de1e30168750060f2c0846d77fd7ec0fdf8a99fe93545c0c09e8d17fca922fef9b')

debs=('aria' 'chipsounds' 'chipspeech' 'sforzando' 'tablewarp2')

package_plogue-aria() {
	# from deb: libgtk-3-0,libglib2.0-0,libpango-1.0-0,libcairo2,libpangocairo-1.0-0,curl
	depends=('gtkmm3' 'glib2' 'pango' 'cairo' 'curl' 'zenity')
	optdepends=('libsndfile: Adds support for more audio formats')

	mkdir -p "$srcdir/${pkgname}"
	cd "$srcdir/${pkgname}"
	ar x "$srcdir/LINUX_plogue-chipspeech_${_dirver}_x86_64/${pkgname}_${debver}_amd64.deb"
	tar -C "${pkgdir}" -xJf "$srcdir/$pkgname/data.tar.xz"
	install -Dm 644 "${pkgdir}/usr/share/doc/${pkgname}/copyright" "${pkgdir}/usr/share/licenses/${pkgname}/copyright"
}

package_plogue-chipspeech() {
	depends=('plogue-aria')

	mkdir -p "$srcdir/${pkgname}"
	cd "$srcdir/${pkgname}"
	ar x "$srcdir/LINUX_plogue-chipspeech_${_dirver}_x86_64/${pkgname}_${debver}_amd64.deb"
	tar -C "${pkgdir}" -xJf "$srcdir/$pkgname/data.tar.xz"
	install -Dm 644 "${pkgdir}/usr/share/doc/${pkgname}/copyright" "${pkgdir}/usr/share/licenses/${pkgname}/copyright"
}

package_plogue-chipsounds() {
	depends=('plogue-aria')

	mkdir -p "$srcdir/${pkgname}"
	cd "$srcdir/${pkgname}"
	ar x "$srcdir/LINUX_plogue-chipsounds_${_dirver}_x86_64/${pkgname}_${debver}_amd64.deb"
	tar -C "${pkgdir}" -xJf "$srcdir/$pkgname/data.tar.xz"
	install -Dm 644 "${pkgdir}/usr/share/doc/${pkgname}/copyright" "${pkgdir}/usr/share/licenses/${pkgname}/copyright"
}

package_plogue-sforzando() {
	depends=('plogue-aria')

	mkdir -p "$srcdir/${pkgname}"
	cd "$srcdir/${pkgname}"
	ar x "$srcdir/LINUX_plogue-sforzando_${_dirver}_x86_64/${pkgname}_${debver}_amd64.deb"
	tar -C "${pkgdir}" -xJf "$srcdir/$pkgname/data.tar.xz"
	install -Dm 644 "${pkgdir}/usr/share/doc/${pkgname}/copyright" "${pkgdir}/usr/share/licenses/${pkgname}/copyright"
}
package_plogue-tablewarp2() {
	depends=('plogue-aria')

	mkdir -p "$srcdir/${pkgname}"
	cd "$srcdir/${pkgname}"
	ar x "$srcdir/LINUX_plogue-sforzando_${_dirver}_x86_64/${pkgname}_${debver}_amd64.deb"
	tar -C "${pkgdir}" -xJf "$srcdir/$pkgname/data.tar.xz"
	install -Dm 644 "${pkgdir}/usr/share/doc/${pkgname}/copyright" "${pkgdir}/usr/share/licenses/${pkgname}/copyright"
}

